Import('RTT_ROOT')
from building import *
from os.path import join

LOCAL_CCFLAGS = '-DPC_HOSTED=0 -DNO_LIBOPENCM3 -DSTDIO_NEWLIB_H'
LOCAL_CCFLAGS += ' -DENABLE_CORTEXAR=1 -DENABLE_CORTEXM=1 -DENABLE_RISCV=1 -DENABLE_XILINX=1'

# get current directory
cwd = GetCurrentDir()

# The set of source files associated with this SConscript file.

src = Glob(join('blackmagic', 'src', '*.c'), exclude=[join('blackmagic', 'src', 'main.c'), join('blackmagic', 'src', 'rtt.c'), join('blackmagic', 'src', 'memwatch.c')])

# bit-banging
src += [ join('blackmagic', 'src', 'platforms', 'common', 'jtagtap.c'), join('blackmagic', 'src', 'platforms', 'common', 'swdptap.c') ]

# targets
src += Glob(join('blackmagic', 'src', 'target', '*.c'), exclude=[join('blackmagic', 'src', 'target', 'jtagtap_generic.c'), join('blackmagic', 'src', 'target', 'swdptap_generic.c')])
src += Glob(join('port', '*.c'), exclude=[join('port', 'rtt_if.c')])

# rtt
if GetDepend(['BLACKMAGIC_ENABLE_RTT']):
    src += [join('blackmagic', 'src', 'rtt.c')]
    src += [join('port', 'rtt_if.c')]
    LOCAL_CCFLAGS += ' -DENABLE_RTT=1'

if GetDepend(['BLACKMAGIC_ENABLE_RTT_IDENT']):
    LOCAL_CCFLAGS += ' -DRTT_IDENT=${BLACKMAGIC_RTT_IDENT}'

# watchpoints
if GetDepend(['BLACKMAGIC_ENABLE_MEMWATCH']):
    src += [join('blackmagic', 'src', 'memwatch.c')]
    LOCAL_CCFLAGS += ' -DENABLE_MEMWATCH=1'

# gdb no ack
if GetDepend(['BLACKMAGIC_ENABLE_NO_ACK_MODE']):
    LOCAL_CCFLAGS += ' -DADVERTISE_NOACKMODE=1'

path = [join(cwd, 'port')]
path += [join(cwd, 'blackmagic', 'src')]
path += [join(cwd, 'blackmagic', 'src', 'include')]
path += [join(cwd, 'blackmagic', 'src', 'target')]

group = DefineGroup('blackmagic', src, depend = ['PKG_USING_BLACKMAGIC'], CPPPATH = path, LOCAL_CCFLAGS = LOCAL_CCFLAGS)

Return('group')
